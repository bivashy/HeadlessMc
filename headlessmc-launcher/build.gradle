plugins {
    id 'application'
}

def MAIN_CLASS = 'me.earth.headlessmc.launcher.Main'
application {
    mainClass = MAIN_CLASS
}

repositories {
    maven {
        name 'OpenAuthMaven'
        url 'https://litarvan.github.io/maven'
    }
}

run {
    standardInput = System.in
}

configurations {
    includeJarInResources
}

dependencies {
    jarLibsApi project(':headlessmc-api')
    jarLibsApi project(':headlessmc-commons')
    jarLibsApi project(':headlessmc-lwjgl')

    jarLibs group: 'org.ow2.asm', name: 'asm', version: '9.6'
    jarLibs group: 'org.ow2.asm', name: 'asm-tree', version: '9.6'
    jarLibs group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    jarLibs group: 'fr.litarvan', name: 'openauth', version: '1.1.6'

    includeJarInResources ('net.kunmc.lab:forgecli:1.1.0:all') {
        // this is to make it build, jar will still have legacy classes
        exclude module: 'legacy'
        exclude module: 'asm'
        exclude module: 'asm-tree'
    }

    testImplementation(testFixtures(project(':headlessmc-commons')))
    testImplementation 'org.apache.logging.log4j:log4j-api:2.22.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.22.0'
}

processResources {
    into('headlessmc') {
        from project(':headlessmc-runtime')['shadowJar']
        from project(':headlessmc-lwjgl')['jar']
        from configurations.includeJarInResources
        rename 'forgecli(.*).jar', 'forge-cli.jar'
        rename 'headlessmc-lwjgl(.*).jar', 'headlessmc-lwjgl.jar'
        rename 'headlessmc-runtime(.*).jar', 'headlessmc-runtime.jar'
    }
}

jar {
    //noinspection GroovyAssignabilityCheck
    manifest {
        attributes('Main-Class': MAIN_CLASS)
    }

    exclude("module-info.class")
    exclude("META-INF/maven/com.google.code.gson/**")
}

// shadowJar always explodes the contents of jars and cannot distinguish between
// a dependency and a jar in our resources, this works around that.
// https://github.com/johnrengelman/shadow/issues/111#issuecomment-317233031
tasks.register('shadowBugWorkaround', Jar) {
    dependsOn jar
    dependsOn project(':headlessmc-runtime')['shadowJar']
    dependsOn project(':headlessmc-lwjgl')['jar']

    destinationDirectory.set(layout.buildDirectory.dir('shadow-bug-workaround'))

    into ('headlessmc') {
        from configurations.includeJarInResources
        from project(':headlessmc-runtime')['shadowJar']
        from project(':headlessmc-lwjgl')['jar']

        rename 'forgecli(.*).jar', 'forge-cli.jar'
        rename 'headlessmc-lwjgl(.*).jar', 'headlessmc-lwjgl.jar'
        rename 'headlessmc-runtime(.*).jar', 'headlessmc-runtime.jar'
    }
}

shadowJar {
    dependsOn shadowBugWorkaround
    from shadowBugWorkaround

    exclude("META-INF/maven/com.google.code.gson/**")
    exclude("META-INF/versions/**")
    exclude("headlessmc/META-INF/**")
    exclude("me/earth/headlessmc/runtime/**")
    exclude("dev/xdark/**")
    exclude("net/kunmc/lab/**")
    exclude("module-info.class")
    exclude("LICENSE")
}
